Bash {
  Script = (Shebang "\n")? ScriptCode
  ScriptCode = Cmd | NullCmd
  Shebang = "#!/bin/bash"
  Cmd = SequenceCmd
      | IfCommand
      | PipeCmd
      | assignment
      | comment
      | CmdWithComment
      | SimpleCmd

  IfCommand
      = IfCase ElseIfThen* ElseCase? EndIf

  IfCase
      = ifwithspace Conditional semicolon thenwithspace SimpleCmd
  ElseIfThen
      = semicolon elifwithspace Conditional semicolon thenwithspace SimpleCmd
  ElseCase
      = semicolon elsewithspace SimpleCmd
  EndIf = semicolon "fi"

  Conditional
      = "[" bashword BinaryOp bashword "]"  -- binary
      | "[" UnaryOp bashword "]"            -- unary
      | SimpleCmd                           -- cmd

  /* Binary operators */
  BinaryOp
      = Equal | NotEqual | LessThan | GreaterThan | LessThanEq | GreaterThanEq
  Equal = "==" | "=" | "-eq"
  NotEqual = "!=" | "-ne"
  LessThan = "-lt" | "<"
  GreaterThan = "-gt" | ">"
  LessThanEq = "-le" | "<="
  GreaterThanEq (foo)
      = "-ge" | ">="

  /* Unary operators */
  UnaryOp = "-z" | "-n" // TODO: add more unary ops, like -f, -d

  ifwithspace (foo)
      = "if" space+
  elifwithspace = "elif" space+
  elsewithspace = "else" space+
  thenwithspace = "then" allwhitespace+

  SequenceCmd
      = Cmd semicolon Cmd
      | Cmd semicolon ""

  PipeCmd = Cmd "|" Cmd

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = BasicCmd
      | CdCmd
      | PwdCmd
      | LsCmd
      | FindCmd
      | CatCmd
      | WhichCmd
      /* | TestCmd */
      | EchoCmd
      | PushdCmd
      | PopdCmd
      | DirsCmd
      | LnCmd
      | ExitCmd
      | ChmodCmd
      | TouchCmd
      | ExecCmd

  BasicCmd = BasicName options? bashword+
  CdCmd    = "cd" bashword?
  PwdCmd   = "pwd"
  /* TestCmd = // TODO */
  FindCmd  = "find"  bashword+
  LsCmd    = "ls"    options? bashword*
  CatCmd   = "cat" bashword+
  WhichCmd = "which" bashword
  EchoCmd  = "echo" bashword+
  PushdCmd = "pushd" options? bashword?
  PopdCmd  = "popd" options? bashword?
  DirsCmd  = "dirs" (options | bashword)?
  LnCmd    = "ln" options? bashword bashword
  ExitCmd  = "exit" alnum+
  ChmodCmd = "chmod" bashword bashword
  TouchCmd = "touch" options? bashword
  ExecCmd  = CmdName Arglist

  BasicName
      = "cp"
      | "rm"
      | "mkdir"
      | "mv"
      | "sed"
      | "grep"

  CmdName = bashword
  Arglist = bashword*
  NullCmd = ""
  comment = "#" (~"\n" any)*
  options = "-" letter*

  bashword
      = stringLiteral
      | reference
      | bareWord

  reference
      = "$" id -- simple
      | "${" id "}" -- smart

  bareWord
      = (~badchars any)+

  stringLiteral
    = singleString
    | doubleString

  singleString = "'" notSingleQuote* "'"
  doubleString = "\"" notDoubleQuote* "\""

  notSingleQuote
    = (~("'" | "\\") any)
    | "\\" any -- escape

  notDoubleQuote
    = (~("\"" | "\\") any)
    | "\\" any -- escape

  badchars = allwhitespace | "|" | ";" | ">" | "#" | "'" | "\""
  assignment = id "=" bashword?
  id = alnum+
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon = (";" | "\n")+
}
