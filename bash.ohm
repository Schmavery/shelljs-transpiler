Bash {
  Script = (Shebang "\n")? ScriptCode
  ScriptCode = Cmd | NullCmd
  Shebang = "#!/bin/bash"
  Cmd = IfCommand
      | SequenceCmd
      | PipeCmd
      | assignment
      | comment
      | CmdWithComment
      | SimpleCmd
  IfCommand
      = IfTwoArm
      | IfOneArm
  IfOneArm = IfThen SimpleCmd EndIf
  IfTwoArm = IfThen SimpleCmd Else SimpleCmd EndIf
  IfThen = ifwithspace SimpleCmd semicolon "then"
  Else = semicolon "else"
  EndIf = semicolon "fi"
  ifwithspace = "if" space+
  spaceyfi = space+ "fi"
  SequenceCmd = Cmd semicolon Cmd
      | Cmd semicolon NullCmd
  PipeCmd = Cmd "|" Cmd

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = CdCmd
      | PwdCmd
      | LsCmd
      | FindCmd
      | CpCmd
      | RmCmd
      | MvCmd
      | MkdirCmd
      | CatCmd
      | SedCmd
      | GrepCmd
      | WhichCmd
      /* | TestCmd */
      | EchoCmd
      | PushdCmd
      | PopdCmd
      | DirsCmd
      | LnCmd
      | ExitCmd
      | ChmodCmd
      | TouchCmd
      | ExecCmd

  CdCmd = "cd" bashword?
  PwdCmd = "pwd"
  LsCmd = "ls" options? bashword*
  FindCmd = "find" bashword+
  CpCmd = "cp" options? bashword+
  RmCmd = "rm" options? bashword+
  MvCmd = "mv" bashword+
  MkdirCmd = "mkdir" options? bashword+
  /* TestCmd = // TODO */
  CatCmd = "cat" bashword+
  SedCmd = "sed" options? bashword+
  GrepCmd = "grep" options? bashword+
  WhichCmd = "which" bashword
  EchoCmd = "echo" bashword+
  PushdCmd = "pushd" options? bashword?
  PopdCmd = "popd" options? bashword?
  DirsCmd = "dirs" (options | bashword)?
  LnCmd = "ln" options? bashword bashword
  ExitCmd = "exit" alnum+
  ChmodCmd = "chmod" bashword bashword
  TouchCmd = "touch" options? bashword
  ExecCmd = CmdName Arglist

  CmdName = bashword
  Arglist = bashword*
  NullCmd = ""
  comment = "#" (~"\n" any)*
  options = "-" letter*

  bashword
      = stringLiteral
      | nonString

  nonString
      = (~badchars any)+

  stringLiteral = "\"" anychar* "\""
                | "'" anychar* "'"

  /* TODO make sure this works with more characters */
  anychar = allwhitespace | alnum | "|" | ";" | ">" | "<" | "#" | "(" | ")"
          | "*" | "&" | "^" | "%" | "@" | "!"

  badchars = allwhitespace | "|" | ";" | ">" | "#"
  assignment = id "=" bashword?
  id = alnum+
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon = ";" | "\n"
}
