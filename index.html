<!DOCTYPE html>
<html>
<head>
<title>Bash to ShellJS Translator</title>
<style>
@font-face {
  font-family: Junction;
  src: url("fonts/junction-regular/Junction.otf") format("opentype");
}
blockquote {
  font-style: italic;
  padding: 10px;
}
#hint-box {
  position: absolute;
  z-index: 1;
  right: 10px;
  padding: 5px;
  float: right;
  width: 10%;
}
p.hint-text {
  text-align: left;
  font-style: italic;
  font-size: 10pt;
}
table {
  margin-left: auto;
  margin-right: auto;
}
body, h1, p {
  text-align: center;
  font-family: Junction;
}
h2 {
  font-style: italic;
}
.CodeMirror {
  text-align: left;
  width: 50em;
  border: solid;
  border-color: lightgrey;
}
#checkbox1 {
  text-align: center;
}
.code-box {
  height: auto;
  display: table;
  width: auto;
  display: inline-block;
  margin: 10px;
}
.container {
  height: 100%;
  float: right;
  margin-top: 20px;
  margin-left: 10px;
}
button.selector {
  position: relative;
}
</style>
<script src="lib/ohm/dist/ohm.min.js"></script>
<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js"></script>
<script type="text/javascript" src="lib/codemirror/lib/codemirror.js"></script>
<script type="text/javascript" src="lib/codemirror/mode/shell/shell.js"></script>
<script type="text/javascript" src="lib/codemirror/mode/javascript/javascript.js"></script>
<link rel="stylesheet" type="text/css" href="lib/codemirror/lib/codemirror.css">
<script type="text/ohm-js">
Bash {
  Script
      = (Shebang)? allwhitespace* CmdSequence? semicolon*
  CmdSequence = NonemptyListOf<Cmd, semicolon>
  Shebang = "#!" (~"sh" any)* "sh\n"

  Cmd = NoSemicolonCmd
      | SemicolonCmd
      | ("" ~keyword)

  NoSemicolonCmd
      = FunctionDecl
      | CodeBlock
      | IfCommand
      | comment
      | ForCommand
      | WhileCommand
      | CmdWithComment

  SemicolonCmd
      = PipeCmd
      | Export
      | Assignment
      | SimpleCmd

  IfCommand
      = IfCase ElseIfThen* ElseCase? EndIf

  IfCase
      = "if" #(allwhitespace+) Conditional semicolon "then" #(allwhitespace+) CmdSequence
  ElseIfThen
      = semicolon "elif" #(allwhitespace+) Conditional semicolon "then" #(allwhitespace+) CmdSequence
  ElseCase
      = semicolon "else" #(allwhitespace+) CmdSequence
  EndIf = semicolon "fi"

  Conditional
      = TestCmd   -- test
      | SimpleCmd -- cmd

  ForCommand
      = "for" "((" ControlStruct "))" semicolon "do" #(allwhitespace+) CmdSequence Done
          -- c_style
      | "for" id "in" (Call | arrayReference) semicolon "do" #(allwhitespace+) CmdSequence Done          -- for_each

  ControlStruct
      = Assignment ";" id BinaryOp Bashword ";" (~")" Bashword)

  WhileCommand
      = "while" #(allwhitespace+) Conditional semicolon "do" #(allwhitespace+) CmdSequence Done

  Done = semicolon "done"

  FunctionDecl
      = ("function" #(space+))? id_std "()"  allwhitespace* CodeBlock // "()"
      | "function"  #(space+)   id_std "()"? allwhitespace* CodeBlock // "function"

  CodeBlock
      = "{" allwhitespace* CmdSequence allwhitespace* "}"

  BinaryOp (Binary infix operator)
      = "=="  | "=" | "-eq"
      | "!="  | "-ne"
      | "-lt" | "\\<"
      | "-gt" | "\\>"
      | "-le"
      | "-ge"

  UnaryOp (Unary operator)
      = "-z" | "-n" | "-b" | "-c" | "-d" | "-e" | "-f" | "-L" | "-p" | "-S"

  SequenceCmd
      = NoSemicolonCmd semicolon Cmd -- nosemicolon
      | Cmd semicolon Cmd -- std
      | NoSemicolonCmd semicolon -- noscNull
      | Cmd semicolon -- null

  PipeCmd = (PipeCmd | SimpleCmd) "|" allwhitespace* (PipeCmd | SimpleCmd)

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = SimpleCmdBase Redirect* "&"?

  Redirect
      = ">" Bashword
      | ">>" Bashword

  SimpleCmdBase
      = TestCmd
      | CmdName Bashword* -- std

  TestCmd
      = "test " "!"? UnaryOp Bashword               -- unary
      | "test " "!"? Bashword BinaryOp Bashword     -- binary
      | TestCmd_unaryBracket
      | TestCmd_binaryBracket
      | TestCmd_str
  TestCmd_unaryBracket
      = "[[" "!"? UnaryOp Bashword "]]"
      | "[" "!"? UnaryOp Bashword "]"
  TestCmd_binaryBracket
      = "[[" "!"? Bashword BinaryOp Bashword "]]"
      | "[" "!"? Bashword BinaryOp Bashword "]"
  TestCmd_str
      = "[[" "!"? Bashword "]]"
      | "[" "!"? Bashword "]"

  CmdName = (~keyword Bashword)
  comment = space* "#" (~"\n" any)*
  /* modOptions = ("-" |"+" ) letter+ */

  Bashword
      = Call
      | ArrayLiteral
      | arrayLength
      | arrayReference
      | stringLiteral
      | number
      | bareWord

  ArrayLiteral
      = "(" allwhitespace* (Bashword allwhitespace*)* ")"

  Call
      = "$(" Cmd ")"
      | "\"$(" Cmd ")\""
      | "`" Cmd "`"
      | "\"`" Cmd "`\""

  arrayReference
      = "${" id "[@]}"
      | "\"${" id "[@]}\""

  arrayLength
      = "${#" id "[@]}"

  reference
      = "$" id -- simple
      | "${" id "}" -- wrapped
      | "${" id ":" digit+ ":"? digit* "}" -- substr
      | "${" id ("//" | "/") alnum* "/"? alnum* "}" -- substit
      | "${#" id "}" -- length

  number
      = "-"? digit+

  bareWord
      = barewordChar+

  barewordChar
      = (~(badchars | "\\") (reference | any)) -- normal
      | "\\" any -- escape

  keyword
      = keywordRoot ~alnum

  keywordRoot
      = "if" | "then" | "else" | "elif" | "fi" | "for" | "done" | "do" | "function"

  stringLiteral
      = singleString
      | doubleString

  singleString = "'" notSingleQuote* "'"
  doubleString = "\"" notDoubleQuote* "\""

  notSingleQuote
      = (~"'" any)
      | "\\" any -- escape

  notDoubleQuote
      = (~("\"" | "\\") (reference | any))
      | "\\" any -- escape

  badchars = allwhitespace | "|" | ";" | ">" | "#" | "'" | "\"" | "`" | "(" | ")" | "&" | "{" | "}"
  Export
      = "export " Assignment -- assign
      | "export " id -- bare
  Assignment
      = ("local " | "readonly ")? #(id_std "=" ~space) Bashword?

  id  = (~keyword (letter | "_") (alnum | "_")*) -- std
      | digit+ -- arg
      | "?" -- error
      | "#" -- argLength
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon
      = ";"* "\n" -- both
      | ";"+
}
</script>
<script src="src/semantics.js"></script>
<script src="src/hints.js"></script>
<script>
var editor;
var jscode;
$(document).ready(function(){
  // code here
  var code = $('.codemirror-textarea')[0];
  editor = CodeMirror.fromTextArea(code, {
    mode: 'shell',
    lineNumbers: true
  });
  var output = $('.codemirror-textarea')[1];
  jscode = CodeMirror.fromTextArea(output, {
    readOnly: true,
    mode: 'javascript',
    lineNumbers: true
  });
});
function selectAll(id) {
  document.getElementById(id).select();
}
var bash = ohm.grammarFromScriptElement();
var s = bash.semantics();
var errMessage;
var warned = false;
s.addOperation(
  'toJS(indent)',
  source2sourceSemantics);

function loadTranslatedText() {
  // Note: must keep `globalInclude` in global namespace for now
  globalInclude.value = document.getElementById('checkbox1').checked
  var script = editor.getValue().trim() + '\n';
  var m = bash.match(script);
  errMessage = m.failed() ? m.message : null;
  warned = false;
  if (!errMessage) {
    var n = s(m);
    var replacementText = n.toJS(0).trimRight();
    jscode.setValue(replacementText);
  }
}

$('document').ready(function() {
  editor.on('change', loadTranslatedText);
  $('#checkbox1').change(loadTranslatedText);
  var timeout;
  $('#bashcode').keypress(function() {
    if(timeout) {
      clearTimeout(timeout);
      timeout = null;
    }
    timeout = setTimeout(function() {
      if (errMessage) {
        console.error(errMessage);
        warned = true;
      }
    }, 3000);
  });
  loadTranslatedText();

  // Load in a hint
  var idx = Math.floor(Math.random() * hints.length);
  document.getElementById('hint-body').innerHTML = hints[idx];
});
</script>
</head>
<body>
<h1>Bash to ShellJS Translator</h1>
<blockquote
cite="https://github.com/shelljs/shelljs#shelljs---unix-shell-commands-for-nodejs">
"Say goodbye to those gnarly Bash scripts!"
-- @arturadib, <a
href="https://github.com/shelljs/shelljs#shelljs---unix-shell-commands-for-nodejs">ShellJS
README</a>
</blockquote>
<p>Like it? Check out the project <a href="https://github.com/nfischer/shelljs-transpiler">on Github</a></p>
<input type="checkbox" name="globalInclude" id="checkbox1" checked="">Include in global namespace <i><a href="http://github.com/shelljs/shelljs#global-vs-local">What is this?</a></i>
<br>
<div id="hint-box">
<p class="hint-text">Hint:</p>
<p class="hint-text" id="hint-body"></p>
</div>
<table>
<tr>
<td>
<div class="code-box">
<h2>Shell Input Code</h2>
<textarea id="bashcode" class="codemirror-textarea">
#!/bin/bash
echo "Type something, paste in some code, or drag-and-drop a shell script"
</textarea>
<div class="container">
<input type="button" class="selector" value="Select this script" onclick="editor.execCommand('selectAll'); editor.focus()" id="button1">
</div>
</div>
</td>
</tr>
<tr>
<td>
<div class="code-box">
<h2>Generated ShellJS Output</h2>
<textarea id="jscode" class="codemirror-textarea">
</textarea>
<div class="container">
<input type="button" class="selector" value="Select this script" onclick="jscode.execCommand('selectAll'); jscode.focus()" id="button2">
</div>
</div>
</td>
</tr>
</table>
</body>
</html>
