<!DOCTYPE html>
<html>
<head>
<title>Bash to ShellJS Translator</title>
<script src="node_modules/ohm-js/dist/ohm.js"></script>
<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.7.2/jquery.min.js"></script>
<script type="text/ohm-js">
Bash {
  Script
      = (comment | allwhitespace)* (Shebang allwhitespace+)? ScriptCode?
  ScriptCode = Cmd | NullCmd
  Shebang = "#!" (~"sh" any)* "sh"
  Cmd = SequenceCmd
      | NoSemicolonCmd
      | PipeCmd
      | Assignment
      | SimpleCmd

  NoSemicolonCmd
      = IfCommand
      | comment
      | ForCommand
      | WhileCommand
      | CmdWithComment

  BlockedCmd (a command that can appear inside a block)
      = comment
      /* = IfCommand */
      /* | ForCommand */
      /* | WhileCommand */
      /* | CmdWithComment */
      /* | PipeCmd */
      /* | Assignment */
      | SimpleCmd

  IfCommand
      = IfCase ElseIfThen* ElseCase? EndIf

  IfCase
      = ifwithspace Conditional semicolon thenwithspace BlockedCmd
  ElseIfThen
      = semicolon elifwithspace Conditional semicolon thenwithspace SimpleCmd
  ElseCase
      = semicolon elsewithspace SimpleCmd
  EndIf = semicolon "fi"

  Conditional
      = TestCmd   -- test
      | SimpleCmd -- cmd

  ForCommand
      = ForControl Done

  ForControl
      = "for" "((" ControlStruct "))" semicolon dowithspace SimpleCmd -- c_style
      | "for" id "in" Call semicolon dowithspace SimpleCmd -- for_each

  ControlStruct
      = Assignment ";" id BinaryOp Bashword ";" (~")" Bashword)

  WhileCommand
      = WhileControl Done

  WhileControl
      = whilewithspace Conditional semicolon dowithspace SimpleCmd

  whilewithspace = "while" space+
  dowithspace = "do" allwhitespace+

  Done = semicolon "done"

  BinaryOp (Binary operator)
      = Equal | NotEqual | LessThan | GreaterThan | LessThanEq | GreaterThanEq
  Equal         = "==" | "=" | "-eq"
  NotEqual      = "!=" | "-ne"
  LessThan      = "-lt" | "<"
  GreaterThan   = "-gt" | ">"
  LessThanEq    = "-le" | "<="
  GreaterThanEq = "-ge" | ">="

  UnaryOp (Unary operator)
      = "-z" | "-n" | "-b" | "-c" | "-d" | "-e" | "-f" | "-L" | "-p" | "-S"

  ifwithspace   = "if"   allwhitespace+
  elifwithspace = "elif" allwhitespace+
  elsewithspace = "else" allwhitespace+
  thenwithspace = "then" allwhitespace+

  SequenceCmd
      = NoSemicolonCmd semicolon Cmd -- nosemicolon
      | Cmd semicolon Cmd -- std
      | NoSemicolonCmd semicolon -- noscNull
      | Cmd semicolon -- null

  PipeCmd = Cmd "|" Cmd

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = SimpleCmdBase Redirect*

  Redirect
      = ">" Bashword
      | ">>" Bashword

  SimpleCmdBase
      = BasicCmd
      | CdCmd
      | PwdCmd
      | LsCmd
      | FindCmd
      | CatCmd
      | WhichCmd
      | TestCmd
      | EchoCmd
      | PushdCmd
      | PopdCmd
      | DirsCmd
      | LnCmd
      | ExitCmd
      | ChmodCmd
      | TouchCmd
      | SedCmd
      | ExecCmd

  BasicCmd = BasicName options? Bashword+
  CdCmd    = "cd" Bashword?
  PwdCmd   = "pwd"
  TestCmd
      = "test " UnaryOp Bashword           -- unary
      | "test " Bashword BinaryOp Bashword -- binary
      | "[" UnaryOp Bashword "]"           -- unaryBracket
      | "[" Bashword BinaryOp Bashword "]" -- binaryBracket
  FindCmd  = "find "  Bashword+
  LsCmd    = "ls"     options? Bashword*
  CatCmd   = "cat"    Bashword*
  WhichCmd = "which " Bashword
  EchoCmd  = "echo "  Bashword+
  PushdCmd = "pushd"  options? Bashword?
  PopdCmd  = "popd"   options? Bashword?
  DirsCmd  = "dirs"   (options | Bashword)?
  LnCmd    = "ln "    options? Bashword Bashword
  ExitCmd  = "exit"   ("-"? digit+)?
  ChmodCmd = "chmod " Bashword Bashword
  TouchCmd = "touch " options? Bashword
  SedCmd   = "sed "   sedRegex Bashword?
  ExecCmd  = CmdName Arglist

  sedRegex = "'s/" (~"/" ~"'" any)* "/" (~"/" ~"'" any)* ("/" "g"?)? "'"
  BasicName
      = "cp "
      | "rm "
      | "mkdir "
      | "mv "
      | "grep "

  CmdName = (~keyword Bashword)
  Arglist = Bashword*
  NullCmd = ""
  comment = space* ("#" ~"!") (~"\n" any)*
  options = "-" letter*

  Bashword
      = Call
      | reference
      | number
      | stringLiteral
      | bareWord

  Call
      = "$(" Cmd ")"
      | "\"$(" Cmd ")\""
      | "`" Cmd "`"
      | "\"`" Cmd "`\""

  reference
      = reference_errCode
      | "$" id -- simple
      | "${" id "}" -- smart
      | "\"$" id "\"" -- quotesimple
      | "\"${" id "}\"" -- quotesmart

  reference_errCode
      = "$?" | "${?}"

  number
      = digit+

  bareWord
      = (~badchars any)+

  keyword
      = keywordRoot ~alnum

  keywordRoot
      = "if" | "then" | "else" | "elif" | "fi" | "for" | "done" | "do"

  stringLiteral
      = singleString
      | doubleString

  singleString = "'" notSingleQuote* "'"
  doubleString = "\"" notDoubleQuote* "\""

  notSingleQuote
      = (~("'" | "\\") any)
      | "\\" any -- escape

  notDoubleQuote
      = (~("\"" | "\\") any)
      | "\\" any -- escape

  badchars = allwhitespace | "|" | ";" | ">" | "#" | "'" | "\"" | "`" | "(" | ")"
  Assignment
      = ("local" | "readonly")? idEqual Bashword?
  idEqual = id "="
  id = (~keyword alnum+)
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon = (";" | "\n")+
}
</script>

<script src="src/semantics.js"></script>
<style>
textarea {
  overflow-x: scroll;
  overflow-y: scroll;
}
</style>
</head>
<body>
<h1>Bash to ShellJS Translator</h1>
<div>
<textarea id="bashcode" spellcheck="false" cols="80" rows="20" wrap="off">
echo "Type something in this box, or paste in a simple shell script"
</textarea>
<input type="checkbox" name="globalInclude" id="checkbox1" checked>Include in global namespace
<script>
var bash = ohm.grammarFromScriptElement();
var s = bash.semantics();
var errMessage;
var warned = false;
s.addOperation(
  'toJS(indent)',
  source2sourceSemantics);

function loadTranslatedText() {
  globalInclude = document.getElementById('checkbox1').checked
  var script = document.getElementById('bashcode').value.trim() + '\n';
  var m = bash.match(script);
  errMessage = m.failed() ? m.message : null;
  warned = false;
  if (!errMessage) {
    var n = s(m);
    var replacementText = n.toJS(0);
    document.getElementById('jscode').innerHTML = replacementText;
  }
}
$('#bashcode').keyup(loadTranslatedText);
$('#checkbox1').change(loadTranslatedText);
$('document').ready(loadTranslatedText);
var timeout;
$('#bashcode').keypress(function() {
  if(timeout) {
    clearTimeout(timeout);
    timeout = null;
  }
  timeout = setTimeout(function() {
    if (errMessage) {
      console.error(errMessage);
      warned = true;
    }
  }, 3000);
});
</script>

</div>
<div>
<textarea id="jscode" spellcheck="false" cols="80" rows="20" wrap="off" readonly>
</textarea>
<input type="button" value="Select all" onclick="selectAll()" id="button1">
</div>
<script>
function selectAll() {
  document.getElementById('jscode').select();
}
</script>
</body>
</html>
