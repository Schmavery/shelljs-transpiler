Bash {
  Script
      = (Shebang "\n")? allwhitespace* CmdSequence? semicolon*
  CmdSequence = NonemptyListOf<Cmd, semicolon>
  Shebang = "#!" (~"sh" any)* "sh"

  Cmd = NoSemicolonCmd
      | SemicolonCmd
      | ("" ~keyword)

  NoSemicolonCmd
      = IfCommand
      | comment
      | ForCommand
      | WhileCommand
      | CmdWithComment

  SemicolonCmd
      = PipeCmd
      | Assignment
      | SimpleCmd

  IfCommand
      = IfCase ElseIfThen* ElseCase? EndIf

  IfCase
      = ifwithspace Conditional semicolon thenwithspace CmdSequence
  ElseIfThen
      = semicolon elifwithspace Conditional semicolon thenwithspace CmdSequence
  ElseCase
      = semicolon elsewithspace CmdSequence
  EndIf = semicolon "fi"

  Conditional
      = TestCmd   -- test
      | SimpleCmd -- cmd

  ForCommand
      = "for" "((" ControlStruct "))" semicolon dowithspace CmdSequence Done
          -- c_style
      | "for" id "in" (Call | arrayReference) semicolon dowithspace CmdSequence Done          -- for_each

  ControlStruct
      = Assignment ";" id BinaryOp Bashword ";" (~")" Bashword)

  WhileCommand
      = whilewithspace Conditional semicolon dowithspace CmdSequence Done

  whilewithspace = "while" space+
  dowithspace = "do" allwhitespace+

  Done = semicolon "done"

  BinaryOp (Binary operator)
      = Equal | NotEqual | LessThan | GreaterThan | LessThanEq | GreaterThanEq
  Equal         = "=="  | "=" | "-eq"
  NotEqual      = "!="  | "-ne"
  LessThan      = "-lt" | "<"
  GreaterThan   = "-gt" | ">"
  LessThanEq    = "-le" | "<="
  GreaterThanEq = "-ge" | ">="

  UnaryOp (Unary operator)
      = "-z" | "-n" | "-b" | "-c" | "-d" | "-e" | "-f" | "-L" | "-p" | "-S"

  ifwithspace   = "if"   allwhitespace+
  elifwithspace = "elif" allwhitespace+
  elsewithspace = "else" allwhitespace+
  thenwithspace = "then" allwhitespace+

  SequenceCmd
      = NoSemicolonCmd semicolon Cmd -- nosemicolon
      | Cmd semicolon Cmd -- std
      | NoSemicolonCmd semicolon -- noscNull
      | Cmd semicolon -- null

  PipeCmd = (PipeCmd | SimpleCmd) "|" allwhitespace* (PipeCmd | SimpleCmd)

  CmdWithComment
      = SimpleCmd comment

  SimpleCmd
      = SimpleCmdBase Redirect*

  Redirect
      = ">" Bashword
      | ">>" Bashword

  SimpleCmdBase
      = BasicCmd
      | CdCmd
      | PwdCmd
      | LsCmd
      | FindCmd
      | CatCmd
      | WhichCmd
      | TestCmd
      | EchoCmd
      | PushdCmd
      | PopdCmd
      | DirsCmd
      | LnCmd
      | ExitCmd
      | ChmodCmd
      | TouchCmd
      | SetCmd
      | SedCmd
      | ExecCmd

  BasicCmd = BasicName options? Bashword+
  CdCmd    = "cd" Bashword?
  PwdCmd   = "pwd"

  TestCmd
      = "test " "!"? UnaryOp Bashword               -- unary
      | "test " "!"? Bashword BinaryOp Bashword     -- binary
      | TestCmd_unaryBracket
      | TestCmd_binaryBracket
  TestCmd_unaryBracket
      = "[[" "!"? UnaryOp Bashword "]]"
      | "[" "!"? UnaryOp Bashword "]"
  TestCmd_binaryBracket
      = "[[" "!"? Bashword BinaryOp Bashword "]]"
      | "[" "!"? Bashword BinaryOp Bashword "]"

  FindCmd  = "find "  Bashword+
  LsCmd    = "ls"     options? Bashword*
  CatCmd   = "cat"    Bashword*
  WhichCmd = "which " Bashword
  EchoCmd  = "echo"   Bashword*
  PushdCmd = "pushd"  options? Bashword?
  PopdCmd  = "popd"   options? Bashword?
  DirsCmd  = "dirs"   (options | Bashword)?
  LnCmd    = "ln "    options? Bashword Bashword
  ExitCmd  = "exit"   Bashword?
  ChmodCmd = "chmod " Bashword Bashword
  TouchCmd = "touch " options? Bashword
  SetCmd   = "set "   modOptions
  SedCmd   = "sed "   sedRegex Bashword*
  ExecCmd  = CmdName Arglist

  sedRegex
      = "'s/" (~"/" ~"'" any)* "/" (~"/" ~"'" any)* ("/" "g"?)? "'"
      | "\"s/" (~"/" ~"\"" any)* "/" (~"/" ~"\"" any)* ("/" "g"?)? "\""
  BasicName
      = "cp "
      | "rm "
      | "mkdir "
      | "mv "
      | "grep "

  CmdName = (~keyword Bashword)
  Arglist = Bashword*
  comment = space* "#" (~"\n" any)*
  options = "-" letter+
  modOptions = ("-" |"+" ) letter+

  Bashword
      = Call
      | ArrayLiteral
      | arrayLength
      | arrayReference
      | stringLiteral
      | number
      | bareWord

  ArrayLiteral
      = "(" allwhitespace* (Bashword allwhitespace*)* ")"

  Call
      = "$(" Cmd ")"
      | "\"$(" Cmd ")\""
      | "`" Cmd "`"
      | "\"`" Cmd "`\""

  arrayReference
      = "${" id "[@]}"
      | "\"${" id "[@]}\""

  arrayLength
      = "${#" id "[@]}"

  reference
      = "$" id -- simple
      | "${" id "}" -- wrapped
      | "${" id ":" digit+ ":"? digit* "}" -- substr
      | "${" id ("//" | "/") alnum* "/"? alnum* "}" -- substit

  number
      = "-"? digit+

  bareWord
      = (~badchars (reference | any))+

  keyword
      = keywordRoot ~alnum

  keywordRoot
      = "if" | "then" | "else" | "elif" | "fi" | "for" | "done" | "do"

  stringLiteral
      = singleString
      | doubleString

  singleString = "'" notSingleQuote* "'"
  doubleString = "\"" notDoubleQuote* "\""

  notSingleQuote
      = (~("'" | "\\") any)
      | "\\" any -- escape

  notDoubleQuote
      = (~("\"" | "\\") (reference | any))
      | "\\" any -- escape

  badchars = allwhitespace | "|" | ";" | ">" | "#" | "'" | "\"" | "`" | "(" | ")"
  Assignment
      = ("local " | "readonly " | "export ")? idEqual Bashword?
  idEqual = id "="
  id = (~keyword (alnum | "_")+) | "?"
  space := " " | "\t"
  allwhitespace = space | "\n"
  semicolon
      = ";"* "\n" -- both
      | ";"+
}
